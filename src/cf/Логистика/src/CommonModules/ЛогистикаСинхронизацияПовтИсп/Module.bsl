#Область СлужебныеПроцедурыИФункции
Функция ВыполнитьПодключениеВнешнейКомпонентыМакет() Экспорт	
	
	СтурктураРезультат = Новый Структура();
	СтурктураРезультат.Вставить("Успех", Ложь);
	СтурктураРезультат.Вставить("ОписаниеОшибки", "");     
	
	СистемнаяИнформация = Новый СистемнаяИнформация();
	Если СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86 Тогда	
		МакетКомпоненты = ПолучитьОбщийМакет("librdkafka_win32");
	ИначеЕсли СистемнаяИнформация.ТипПлатформы = ТипПлатформы.Windows_x86_64 Тогда
		МакетКомпоненты = ПолучитьОбщийМакет("librdkafka_win64");
	Иначе
		СтурктураРезультат.ОписаниеОшибки = "Не поддерживаемая архитектура платформы";
		Возврат СтурктураРезультат;
	КонецЕсли;
	
	Попытка
		РезультатПодключенияВнешнейКомпоненты = ПодключитьВнешнююКомпоненту(ПоместитьВоВременноеХранилище(МакетКомпоненты), "librdkafka", ТипВнешнейКомпоненты.Native);
		Если НЕ РезультатПодключенияВнешнейКомпоненты Тогда
			СтурктураРезультат.ОписаниеОшибки = "Ошибка подключения внешней компоненты";
			Возврат СтурктураРезультат;
		КонецЕсли;
	Исключение
		СтурктураРезультат.ОписаниеОшибки = ОписаниеОшибки();
		Возврат СтурктураРезультат;
	КонецПопытки;
	
	СтурктураРезультат.Успех = Истина;
	
	Возврат СтурктураРезультат;
КонецФункции

// Создать компоненту.
// 
// Параметры:
//  КлючКомпоненты - Строка 
//  Принимает значения "KafkaAdminClient", "KafkaProducer", "KafkaConsumer"
// 
// Возвращаемое значение:
//  Структура - Создать компоненту:
// * Успех - Булево - 
// * ОписаниеОшибки - Строка - 
// * Компонента - Неопределено - 
Функция СоздатьКомпоненту(КлючКомпоненты) Экспорт
	
	СтурктураРезультат = Новый Структура();
	СтурктураРезультат.Вставить("Успех", Ложь);
	СтурктураРезультат.Вставить("ОписаниеОшибки", "");
	СтурктураРезультат.Вставить("Компонента", Неопределено);
	
	
	 СтруктураРезультатПодключения = ВыполнитьПодключениеВнешнейКомпонентыМакет();
	 Если Не СтруктураРезультатПодключения.Успех Тогда
		 СтурктураРезультат.ОписаниеОшибки = СтруктураРезультатПодключения.ОписаниеОшибки;
		Возврат СтурктураРезультат;	 
	 КонецЕсли;
	 
	Попытка
		СтурктураРезультат.Компонента = Новый("AddIn.librdkafka."+КлючКомпоненты);
	Исключение
		СтурктураРезультат.ОписаниеОшибки = ОписаниеОшибки();
		Возврат СтурктураРезультат;
	КонецПопытки;
	
	СтурктураРезультат.Успех = Истина;
	Возврат СтурктураРезультат;
КонецФункции

#КонецОбласти