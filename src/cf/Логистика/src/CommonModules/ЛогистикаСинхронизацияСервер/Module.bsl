#Область ПрограммныйИнтерфейс
// Справочники при записи.
// 
// Параметры:
//  Источник - СправочникОбъект.Номенклатура, СправочникОбъект.Склады - Источник
//  Отказ - Булево - Отказ
Процедура СправочникиПриЗаписи(Источник, Отказ) Экспорт
	
	Если Источник.ДополнительныеСвойства.Свойство("БезРегистрации") И Источник.ДополнительныеСвойства.БезРегистрации Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураУзелОбмена = ПолучитьУзелОбмена(Перечисления.НазначениеУзлаОбмена.Справочники);
	
	ПланыОбмена.ЗарегистрироватьИзменения(СтруктураУзелОбмена.Ссылка, Источник.Ссылка);	
	
КонецПроцедуры

// Документы при записи.
// 
// Параметры:
//  Источник - ДокументОбъект.РасходныйОрдер, ДокументОбъект.ПриходныйОрдер - Источник
//  Отказ - Булево - Отказ
Процедура ДокументыПриЗаписи(Источник, Отказ) Экспорт
	
	Если Источник.ДополнительныеСвойства.Свойство("БезРегистрации") И Источник.ДополнительныеСвойства.БезРегистрации Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураУзелОбмена = ПолучитьУзелОбмена(Перечисления.НазначениеУзлаОбмена.Документы);
	
	ПланыОбмена.ЗарегистрироватьИзменения(СтруктураУзелОбмена.Ссылка, Источник.Ссылка);	
	
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
// Получить узел обмена.
// 
// Параметры:
//  Назначение - ПеречислениеСсылка.НазначениеУзлаОбмена - Назначение
// 
// Возвращаемое значение:
//  Структура
Функция ПолучитьУзелОбмена(Назначение) Экспорт
	
	СтруктураОтвет = Новый Структура("Ссылка,АдресСервера,Топик,Назначение,Склад,ИдентификаторСклада,ЦентральныйОфис");
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СинхронизацияКафка.Ссылка,
	|	СинхронизацияКафка.АдресСервера,
	|	СинхронизацияКафка.Топик,
	|	СинхронизацияКафка.Склад,
	|	СинхронизацияКафка.Склад.ИдентификаторСклада КАК ИдентификаторСклада,
	|	СинхронизацияКафка.ЦентральныйОфис
	|ИЗ
	|	ПланОбмена.СинхронизацияКафка КАК СинхронизацияКафка
	|ГДЕ
	|	СинхронизацияКафка.Назначение = &Назначение
	|	И НЕ СинхронизацияКафка.ЭтотУзел";
	Запрос.УстановитьПараметр("Назначение", Назначение);
	Результат = Запрос.Выполнить();
	Если Результат .Пустой() Тогда
		НовыйЭлемент = ПланыОбмена.СинхронизацияКафка.СоздатьУзел();
		НовыйЭлемент.УстановитьНовыйКод();
		НовыйЭлемент.Наименование = Строка(Назначение);
		НовыйЭлемент.Назначение = Назначение;
		НовыйЭлемент.Записать();
		ЗаполнитьЗначенияСвойств(СтруктураОтвет, НовыйЭлемент);
		СтруктураОтвет.Ссылка = НовыйЭлемент.Ссылка;
		Возврат СтруктураОтвет;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ЗаполнитьЗначенияСвойств(СтруктураОтвет, Выборка);
		Возврат СтруктураОтвет;	
	КонецЕсли;
КонецФункции

Функция ВыгрузитьСправочникиНаСервер() Экспорт
	
	СтруктураУзелОбмена = ПолучитьУзелОбмена(Перечисления.НазначениеУзлаОбмена.Справочники);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	""СправочникОбъект.Номенклатура"" КАК Тип,
	|	НоменклатураИзменения.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Номенклатура.Изменения КАК НоменклатураИзменения
	|ГДЕ
	|	НоменклатураИзменения.Узел = &Узел
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""СправочникОбъект.Склады"" КАК Тип,
	|	СкладыИзменения.Ссылка
	|ИЗ
	|	Справочник.Склады.Изменения КАК СкладыИзменения
	|ГДЕ
	|	СкладыИзменения.Узел = &Узел";
	Запрос.УстановитьПараметр("Узел", СтруктураУзелОбмена.Ссылка);	
	РезультатЗапроса = Запрос.Выполнить();
		
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СтруктураРезультат = ВыгрузитьОбъектыНаСервер(СтруктураУзелОбмена, ВыборкаДетальныеЗаписи);
	
	Возврат СтруктураРезультат;
КонецФункции

Функция ВыгрузитьДокументыНаСервер() Экспорт
	СтруктураУзелОбмена = ПолучитьУзелОбмена(Перечисления.НазначениеУзлаОбмена.Документы);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	""ДокументОбъект.ПриходныйОрдер"" КАК Тип,
	|	ПриходныйОрдерИзменения.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.ПриходныйОрдер.Изменения КАК ПриходныйОрдерИзменения
	|ГДЕ
	|	ПриходныйОрдерИзменения.Узел = &Узел
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	""ДокументОбъект.РасходныйОрдер"" КАК Тип,
	|	РасходныйОрдерИзменения.Ссылка
	|ИЗ
	|	Документ.РасходныйОрдер.Изменения КАК РасходныйОрдерИзменения
	|ГДЕ
	|	РасходныйОрдерИзменения.Узел = &Узел";
	Запрос.УстановитьПараметр("Узел", СтруктураУзелОбмена.Ссылка);	
	РезультатЗапроса = Запрос.Выполнить();
		
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СтруктураРезультат = ВыгрузитьОбъектыНаСервер(СтруктураУзелОбмена, ВыборкаДетальныеЗаписи);
	
	Возврат СтруктураРезультат;
КонецФункции

Функция ЗагрузитьСправочникиССервера() Экспорт
	СтруктураУзелОбмена = ПолучитьУзелОбмена(Перечисления.НазначениеУзлаОбмена.Справочники);
	
	Возврат ЗагрузитьОбъектыНаСервер(СтруктураУзелОбмена);
КонецФункции

Функция ЗагрузитьДокументыССервера() Экспорт
	СтруктураУзелОбмена = ПолучитьУзелОбмена(Перечисления.НазначениеУзлаОбмена.Документы);
	
	Возврат ЗагрузитьОбъектыНаСервер(СтруктураУзелОбмена);
КонецФункции

Функция ВыгрузитьОбъектыНаСервер(СтруктураУзелОбмена, ВыборкаДетальныеЗаписи) Экспорт
	
	Событие = "ОбменЛогистика.ВыгрузкаСправочников";
	
	//создаем компоненту
	СтруктураОтправитель = СоздатьОтправителя();
	Если СтруктураОтправитель.Успех = Ложь ИЛИ СтруктураОтправитель.Компонента = Неопределено Тогда
		Возврат СтруктураОтправитель;
	КонецЕсли;
	
	//инициализируем
	ТаблицаПараметров = Новый ТаблицаЗначений();
	ТаблицаПараметров.Колонки.Добавить("Key");
	ТаблицаПараметров.Колонки.Добавить("Value");
	
	СтрокаПараметр = ТаблицаПараметров.Добавить();
	СтрокаПараметр.Key = "message.timeout.ms";
	СтрокаПараметр.Value = "30000";
	
	СтрокаПараметр = ТаблицаПараметров.Добавить();
	СтрокаПараметр.Key = "compression.codec";
	СтрокаПараметр.Value = "lz4";
	
	СтрокаПараметр = ТаблицаПараметров.Добавить();
	СтрокаПараметр.Key = "request.required.acks";
	СтрокаПараметр.Value = "-1";
		
	Для Каждого СтрокаПараметр ИЗ ТаблицаПараметров Цикл
		СтруктураРезультат = ОтправительУстановитьПараметрТопика(СтруктураОтправитель.Компонента, СтрокаПараметр.Key, СтрокаПараметр.Value);
		Если НЕ СтруктураРезультат.Успех Тогда
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,,СтруктураРезультат.ОписаниеОшибки);
			Возврат СтруктураОтправитель;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураИнициализация = ОтправительИнициализировать(СтруктураОтправитель.Компонента, СтруктураУзелОбмена.АдресСервера, СтруктураУзелОбмена.Топик, 0);
	Если НЕ СтруктураИнициализация.Успех Тогда
		ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,,СтруктураИнициализация.ОписаниеОшибки);
		Возврат СтруктураОтправитель;
	КонецЕсли;
		
	ДанныеНаЗапись = Новый Массив();
	МассивСсылок = Новый Массив();	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		// Вставить обработку выборки ВыборкаДетальныеЗаписи
						
		мЗаголовки = Новый Массив();
		мЗаголовки.Добавить(Новый Структура("type", ВыборкаДетальныеЗаписи.Тип));
			
		ЗаписьJSON = Новый ЗаписьJSON;
		ЗаписьJSON.УстановитьСтроку();
		СериализаторXDTO.ЗаписатьJSON(ЗаписьJSON, ВыборкаДетальныеЗаписи.Ссылка.ПолучитьОбъект());
		СтрокаСообщение = ЗаписьJSON.Закрыть();
		
		СтруктураЗаписи = Новый Структура("Key, Value, Headers",
							XMLСтрока(ВыборкаДетальныеЗаписи.Ссылка), 
							СтрокаСообщение, 
							мЗаголовки);
		ДанныеНаЗапись.Добавить(СтруктураЗаписи);
		
		МассивСсылок.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
	
	//формируем данные для доставки
	РезультатПреобразования = СтруктуруВJSON(ДанныеНаЗапись);   
	СтруктураРезультат = ОтправительЗагрузитьСообщенияИзJSONВСписокНаОтправку(СтруктураОтправитель.Компонента, РезультатПреобразования.СтруктураДанные);
	Если СтруктураРезультат.Успех Тогда
		ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Информация,,,"Успех");
		
		Если МассивСсылок.Количество() > 0 Тогда
			ПланыОбмена.УдалитьРегистрациюИзменений(СтруктураУзелОбмена.Ссылка, МассивСсылок);
		КонецЕсли;
		
	Иначе
		ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,,СтруктураРезультат.ОписаниеОшибки);
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	//отправляем
	СтруктураРезультат = ОтправительДекодироватьКлючСообщенияКакBase64Строку(СтруктураОтправитель.Компонента, Ложь);
	Если Не СтруктураРезультат.Успех Тогда 
		Возврат СтруктураРезультат;
	КонецЕсли;
		
//	СтруктураРезультат = ОтправительДекодироватьТелоСообщенияКакBase64Строку(СтруктураОтправитель.Компонента, Ложь);
//	Если Не СтруктураРезультат.Успех Тогда 
//		Возврат СтруктураРезультат;
//	КонецЕсли;
		
//	СтруктураРезультат = ОтправительДекодироватьЗначенияЗаголовковКакBase64Строку(СтруктураОтправитель.Компонента, Ложь);
//	Если Не СтруктураРезультат.Успех Тогда 
//		Возврат СтруктураРезультат;
//	КонецЕсли;
	
	СтруктураРезультат = ОтправительОтправить(СтруктураОтправитель.Компонента);
	Если Не СтруктураРезультат.Успех Тогда
		Возврат СтруктураРезультат;
	КонецЕсли;

	СтруктураРезультат = ОтправительВсеСообщенияДоставлены(СтруктураОтправитель.Компонента);
	Если Не СтруктураРезультат.Успех Тогда
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	СтруктураРезультат = ОтправительПолучитьJSONОтчетОДоставке(СтруктураОтправитель.Компонента);
	
	
	Возврат СтруктураРезультат;
КонецФункции

Функция ЗагрузитьОбъектыНаСервер(СтруктураУзелОбмена) Экспорт
	
	ИдентификаторСклада = СтруктураУзелОбмена.ИдентификаторСклада;
	
	Событие = "ОбменЛогистика.ЗагрузкаСправочников";
	
	//создаем компоненту
	СтруктураПолучатель = СоздатьПолучателя();
	Если СтруктураПолучатель.Успех = Ложь ИЛИ СтруктураПолучатель.Компонента = Неопределено Тогда
		Возврат СтруктураПолучатель;
	КонецЕсли;
	
	//инициализируем глобальные настройки
	ТаблицаПараметров = Новый ТаблицаЗначений();
	ТаблицаПараметров.Колонки.Добавить("Key");
	ТаблицаПараметров.Колонки.Добавить("Value");
	СтрокаПараметр = ТаблицаПараметров.Добавить();
	СтрокаПараметр.Key =  "message.max.bytes";
	СтрокаПараметр.Value = "10000"; 
		
	СтрокаПараметр = ТаблицаПараметров.Добавить();
	СтрокаПараметр.Key =  "socket.timeout.ms";
	СтрокаПараметр.Value = "30000"; 	
	
	СтрокаПараметр = ТаблицаПараметров.Добавить();
	СтрокаПараметр.Key =  "enable.auto.commit";
	СтрокаПараметр.Value = "false"; 
	
	СтрокаПараметр = ТаблицаПараметров.Добавить();
	СтрокаПараметр.Key =  "auto.commit.interval.ms";
	СтрокаПараметр.Value = "0";
	
	Для Каждого СтрокаПараметр ИЗ ТаблицаПараметров Цикл
		СтруктураРезультат = ПолучательУстановитьПараметрГлобальный(СтруктураПолучатель.Компонента, СтрокаПараметр.Key, СтрокаПараметр.Value);
		Если НЕ СтруктураРезультат.Успех Тогда
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,,СтруктураРезультат.ОписаниеОшибки);
			Возврат СтруктураРезультат;
		КонецЕсли;
	КонецЦикла;
	
	//инициализируем парметры топика
	ТаблицаПараметров.Очистить();
	
	СтрокаПараметр = ТаблицаПараметров.Добавить();
	СтрокаПараметр.Key =  "compression.codec";
	СтрокаПараметр.Value = "lz4"; 
	
	СтрокаПараметр = ТаблицаПараметров.Добавить();
	СтрокаПараметр.Key =  "auto.offset.reset";
	СтрокаПараметр.Value = "smallest";
		
	Для Каждого СтрокаПараметр ИЗ ТаблицаПараметров Цикл
		СтруктураРезультат = ПолучательУстановитьПараметрТопика(СтруктураПолучатель.Компонента, СтрокаПараметр.Key, СтрокаПараметр.Value);
		Если НЕ СтруктураРезультат.Успех Тогда
			ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,,СтруктураРезультат.ОписаниеОшибки);
			Возврат СтруктураРезультат;
		КонецЕсли;
	КонецЦикла;
	
	СтруктураИнициализация = ПолучательИнициализировать(СтруктураПолучатель.Компонента, СтруктураУзелОбмена.АдресСервера, ИдентификаторСклада);
	Если НЕ СтруктураИнициализация.Успех Тогда
		ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,,СтруктураИнициализация.ОписаниеОшибки);
		Возврат СтруктураРезультат;
	КонецЕсли;
		
	МассивТопики = Новый Массив();
	МассивТопики.Добавить(СтруктураУзелОбмена.Топик);
	
	СтруктураРезультат = ПолучательПодписаться(СтруктураПолучатель.Компонента, МассивТопики);
	Если Не СтруктураРезультат.Успех Тогда
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	КоличествоОбъектовЧтение = 1000;
	Таймаут = 3000;
	ЧитатьДвоичныеДанные = Ложь;
	
	СтруктураРезультат = ПолучательЭкранироватьЗначениеСообщения(СтруктураПолучатель.Компонента);
	Если Не СтруктураРезультат.Успех Тогда
		Возврат СтруктураРезультат;	
	КонецЕсли;	
	
	СтруктураРезультат = ПолучательЭкранироватьКлючСообщения(СтруктураПолучатель.Компонента);
	Если Не СтруктураРезультат.Успех Тогда
		Возврат СтруктураРезультат;	
	КонецЕсли;	
	
	СтруктураРезультат = ПолучательЭкранироватьКлючиЗаголовков(СтруктураПолучатель.Компонента);
	Если Не СтруктураРезультат.Успех Тогда
		Возврат СтруктураРезультат;	
	КонецЕсли;	
	
	СтруктураРезультат = ПолучательЭкранироватьЗначенияЗаголовков(СтруктураПолучатель.Компонента);
	Если Не СтруктураРезультат.Успех Тогда
		Возврат СтруктураРезультат;	
	КонецЕсли;	

	
	ДатаНачала = ТекущаяУниверсальнаяДатаВМиллисекундах();
	СтруктураРезультат = ПолучательПрочитатьСписокСообщенийВПул(СтруктураПолучатель.Компонента, КоличествоОбъектовЧтение, Таймаут, 10);
	Если Не СтруктураРезультат.Успех Тогда
		Возврат СтруктураРезультат;;	
	КонецЕсли;	
	
	СтруктураРезультат = ПолучательПолучитьСообщенияИзПулаJSON(СтруктураПолучатель.Компонента, ЧитатьДвоичныеДанные);
	Если Не СтруктураРезультат.Успех Тогда
		Возврат СтруктураРезультат;		
	КонецЕсли;	
		
	ДатаОкончания = ТекущаяУниверсальнаяДатаВМиллисекундах();
	
	СтруктураРезультат = JSONВСтруктуру(СтруктураРезультат.СтрокаСообщения);
	Если Не СтруктураРезультат.Успех Тогда
		Возврат СтруктураРезультат;		
	КонецЕсли;	
	
	Если СтруктураРезультат.МассивДанные.Количество() > 0 Тогда		
		СтруктураРезультатФиксации = ПолучательЗафиксироватьСмещения(СтруктураПолучатель.Компонента);
		Если Не СтруктураРезультатФиксации.Успех Тогда
			Возврат СтруктураРезультатФиксации;
		КонецЕсли;	
	КонецЕсли;
			
	ТаблицаДанные = ПолучательПолучитьТаблицуСообщения();
	Для Каждого ЭлементМассива Из СтруктураРезультат.МассивДанные Цикл
		
		СтрокаТаблицы = ТаблицаДанные.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ЭлементМассива);	
		СтрокаТаблицы.headers = ПолучательПолучитьТаблицуЗаголовки();
		
		Для Каждого СтрокаЗаголовок Из ЭлементМассива.Headers Цикл
			ЗаполнитьЗначенияСвойств(СтрокаТаблицы.headers.Добавить(), СтрокаЗаголовок);
		КонецЦикла;
	КонецЦикла;
	
	ЧтениеJSON = Новый ЧтениеJSON;
	Для Каждого мСтрока ИЗ ТаблицаДанные Цикл
		
		СтрокаЗаголовок = мСтрока.headers.НайтиСтроки(Новый Структура("key","type"));
		Если СтрокаЗаголовок.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаJSON = мСтрока.value;
		
	    ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	    Значение = СериализаторXDTO.ПрочитатьJSON(ЧтениеJSON, Тип(СтрокаЗаголовок[0].value));
	    Значение.ДополнительныеСвойства.Вставить("БезРегистрации", Истина); 
	    Если СтруктураУзелОбмена.Назначение = Перечисления.НазначениеУзлаОбмена.Справочники Тогда
			Значение.Записать();
	    Иначе	    	
	    	//для документов грузим только документы нашего склада, для центрального офиса грузим все
	    	Если СтруктураУзелОбмена.ЦентральныйОфис Или СтруктураУзелОбмена.Склад = Значение.Склад Тогда
	    		Если НЕ СтруктураУзелОбмена.ЦентральныйОфис Тогда
	    			Значение.ДокументОснование = Неопределено;
	    		КонецЕсли;
				Если Значение.Проведен Тогда
					Значение.Записать(РежимЗаписиДокумента.Проведение);
				Иначе
					Значение.Записать(РежимЗаписиДокумента.Запись);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		    
	КонецЦикла;
	ЧтениеJSON.Закрыть();	
	
	Возврат СтруктураРезультат;
КонецФункции

Функция ИнициализацияПросмотр(Адрес) Экспорт
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	//определяем и создаем ВК
	СтруктураРезультатВыполнения = СоздатьКлиентАдминистрирования();
	Если Не СтруктураРезультатВыполнения.Успех Тогда
		СтруктураРезультат.ОписаниеОшибки = СтруктураРезультатВыполнения.ОписаниеОшибки;	
		Возврат СтруктураРезультат;
	КонецЕсли;
		
	СтруктураРезультатВыполнения = КлиентАдминистрированияИнициализация(СтруктураРезультатВыполнения.Компонента, Адрес);
	Если Не СтруктураРезультатВыполнения.Успех Тогда   
		СтруктураРезультат.ОписаниеОшибки = СтруктураРезультатВыполнения.ОписаниеОшибки;	
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
	
КонецФункции

Функция ПолучитьМетаданныеПросмотр(Топик) Экспорт     	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");     
	СтруктураРезультат.Вставить("Данные", Неопределено);
	
	ТаймаутПоУмолчанию = 3000;
	
	СтруктураРезультатВыполнения = КлиентАдминистрированияПолучитьМетаданные(ТаймаутПоУмолчанию, Топик);	
	Если ЗначениеЗаполнено(СтруктураРезультатВыполнения.ОписаниеОшибки) Тогда
		СтруктураРезультат.ОписаниеОшибки = СтруктураРезультатВыполнения.ОписаниеОшибки;	
		Возврат СтруктураРезультат;
	КонецЕсли;                         
	
	СтруктураРезультатВыполнения = JSONВСтруктуру(СтруктураРезультатВыполнения.СтруктураРезультат);       
	Если Не СтруктураРезультатВыполнения.Успех Тогда   
		СтруктураРезультат.ОписаниеОшибки = СтруктураРезультатВыполнения.ОписаниеОшибки;	
		Возврат СтруктураРезультат;
	КонецЕсли;                                              
	
	Если НЕ СтруктураРезультатВыполнения.МассивДанные.Количество() = 1 Тогда
		СтруктураРезультат.ОписаниеОшибки = СтруктураРезультатВыполнения.ОписаниеОшибки;	
		Возврат СтруктураРезультат;	
	КонецЕсли;	
	
	СтруктураРезультат.Данные = СтруктураРезультатВыполнения.МассивДанные[0];  
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;
КонецФункции

#Область Получатель


// Функция - Создать получателя
// 
// Возвращаемое значение:
//   - Структура - (Успех, стрОписнаиеОшибки)
//
//Создает объект получателя в глобальной переменной модуля
Функция СоздатьПолучателя() Экспорт	
	Возврат ЛогистикаСинхронизацияПовтИсп.СоздатьКомпоненту("KafkaConsumer");
КонецФункции
 
// Функция - Получатель установить параметр глобальный
//
// Параметры:
//  Ключ		 - 	Строка - Имя параметра
//  Значение	 - 	Строка - Значение параметра
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если параметр установлен
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Устанавливает параметр глабольного контекста для получателя (Компонента)
//	Пример: Ключ = socket.timeout.ms Значение = 30000  
//!!Для применения установленных параметров - необходимка повторная инициализация получателя с помощью функции "ПолучательИнициализировать"
Функция ПолучательУстановитьПараметрГлобальный(Компонента, Знач Ключ, Знач Значение) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
		
	СтруктураРезультат.Успех = Компонента.SetGlobalConf(Ключ, Значение);
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Получатель установить параметр топика
//
// Параметры:
//  Ключ		 - 	Строка - Имя параметра
//  Значение	 - 	Строка - Значение параметра
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если параметр установлен
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Устанавливает параметр топика для получателя (Компонента) 
//	Пример: Ключ = auto.offset.reset Значение = smallest  
//!!Для применения установленных параметров - необходимка повторная инициализация получателя с помощью функции "ПолучательИнициализировать"
Функция ПолучательУстановитьПараметрТопика(Компонента, Знач Ключ, Знач Значение) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
		
	СтруктураРезультат.Успех = Компонента.SetTopicConf(Ключ, Значение);
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Получатель сбросить параметры
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Сбрасывает установенные параметры топика и параметры глобального контекста (установленные функциями ПолучательУстановитьПараметрГлобальный, ПолучательУстановитьПараметрТопика)
//!!Для применения - необходимка повторная инициализация получателя с помощью функции "ПолучательИнициализировать"
Функция ПолучательСброситьПараметры(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
		
	СтруктураРезультат.Успех = Компонента.ConfReset();
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	Возврат СтруктураРезультат;	
КонецФункции
	
// Функция - Получатель инициализировать
//
// Параметры:
//  СписокБрокеров		 - Строка 	 - Адреса брокеров в формате "10.0.5.187:9092,10.0.5.85:9092,10.0.5.86:9092"
//  ИмяГруппыПолучателей	 - Стркоа	 - Произвольная строка - идентификатор группы получателей
//
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Применяет параметры, установленные функциями "ПолучательУстановитьПараметрГлобальный", "ПолучательУстановитьПараметрТопика", "ПолучательСброситьПараметры" 
//Открывает соединения до брокера, создает управляющие потоки внутри ВК, полностью подготавливает получателя для чтения данных
Функция ПолучательИнициализировать(Компонента, Знач СписокБрокеров, Знач ИмяГруппыПолучателей) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
		
	СтруктураРезультат.Успех = Компонента.Инициализация(СписокБрокеров, ИмяГруппыПолучателей);
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;

	
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Получатель подписаться
//
// Параметры:
//  Топики	 - Массив	 - Массив строк, содержащих имена топиков, на которые необходимо подписаться
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Выполняет подписку на список переданных топиков
//Необходимо вызывать после инициализации
//Предполагает автоматическое распределение партиций топиков на получателей внутри группы
//	(т.е. если подписано на топик несколько получателей, с одной и той же группой - партиции будут распределены между ними брокером автоматически)
Функция ПолучательПодписаться(Компонента, Знач Топики) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	Результат = Компонента.ClearSubscribeList();
	Если Результат <> Истина Тогда
		СтруктураРезультат.ОписаниеОшибки = "Ошибка очистки списка топиков: " + Компонента.ErrorDescription;	
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	Для Каждого Топик Из Топики Цикл 
		Результат = Компонента.AddTopicToSubscribeList(Топик);
		Если Результат <> Истина Тогда
			СтруктураРезультат.ОписаниеОшибки = "Ошибка добавления топика в список: " + Компонента.ErrorDescription;	
			Возврат СтруктураРезультат;
		КонецЕсли;	
	КонецЦикла;
	
	Результат = Компонента.Subscribe();
	Если Результат <> Истина Тогда
		СтруктураРезультат.ОписаниеОшибки = "Ошибка подписания на топики: " + Компонента.ErrorDescription;	
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	СтруктураРезультат.Успех = Истина;	
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Получатель отписаться
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Отписывается от всех топиков, на которые ранее была выполнена подписка с помощью функции "ПолучательПодписаться"
Функция ПолучательОтписаться(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
		
	СтруктураРезультат.Успех = Компонента.Unsubscribe();
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;

	
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Получатель добавить назначение
//
// Параметры:
//  Топик	 - Строка	 -  Имя топика, который необходимо добавить в список назначений
//  чслРаздел	 - Число	 -  Номер раздела топика
//  чслСмещение	 - Число,Неопределено	 - Смещение с которого необходимо начать чтение, если Неопределено - будут получено последнее смещение, закоммиченное в брокер
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Добавляет назначение в специальный список, внутри ВК
//Метод не выполняет назначение, а только добавляет назначение в специальный список внуутри ВК
//Непосредственное назначение выполняется функцией "ПолучательНазначить"
Функция ПолучательДобавитьНазначение(Компонента, Знач Топик, Знач чслРаздел, Знач чслСмещение = Неопределено) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.Успех = Компонента.AddRecordToTopicPartitionList(Топик, чслРаздел, чслСмещение);
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;

	
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Получатель очистить список назначений
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Очищает список назначений внутри ВК, сформированный посредставм вызова функции "ПолучательДобавитьНазначение"
//Функция не выполняет сброс назначений, а только очищает список внутри ВК, назначения сбрасываются функией "ПолучательСброситьНазначения"
Функция ПолучательОчиститьСписокНазначений(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.Успех = Компонента.ClearTopicPartitionList();
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;

	
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Получатель назначить
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Выполняет назначения "марштрутов", для чтения из них данных
//Берет маршруты из внутреннего списка ВК, сформированного вызовами функции "ПолучательДобавитьНазначение"
Функция ПолучательНазначить(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.Успех = Компонента.Assign();
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Получатель сбросить назначения
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Выполняет сброс назначений, сделанных функцией "ПолучательНазначить"
Функция ПолучательСброситьНазначения(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.Успех = Компонента.Unassign();
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Получатель прочитать список сообщения в пул
//		Читает список сообщений из брокера - цикл получения организован внутри ВК, что быстее чем использование метода "ПолучательПрочитатьСообщенияВПул"
//
// Параметры:
//  Количество				 - Число	 - Количество сообщений, которые необходимо получить
//  чслТайимаутНаОдно			 - Число	 - Ожидание оджного сообщения
//  чслМаксимумТаймаутов		 - Число	 - Количество ошибок, в том числе таймаутов, после которого получение будет прервано
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 				- Истина, если успешно
//		чКоличествоПрочитано	- Количество прочитанных сообщений
//		ОписаниеОшибки 		- содержит описание ошибки, в случае неудачи, при выпонении операции
//  
//Получает сообщения из брокера и помещает их во внутренний пул ВК, получение выполняется из "маршрутов", которые назначены получателю (c помощью фунцкии "ПолучательНазначить") 
//или из разделов назначенных получателю брокером (для топиков, на которые выполнялась подписка с помощью функции "ПолучательПодписаться")
//
//
//При подписании на топик - мы читаем из всех его партиций, и в какой-либо могут закочиться сообщения,
// а в любой другой - нет, при этом при чтении из той, где сообщения закончились - мы получим таймаут
// поэтому если мы подписывались на топик, а не назначали партицию для чтения - имеет смысл после первого таймаута
// - делать еще попытки, также таймауты могут быть при перебалансировке нагрузки брокером - RebalanceCb 
// - при подписке на топик еще одного получателя с такой же группой, как у текущего - партиции будут перераспределены
// брокером между получателями
Функция ПолучательПрочитатьСписокСообщенийВПул(Компонента, Знач Количество, Знач чслТайимаутНаОдно, Знач ErrorCountToInterrupt = 1) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("булТаймаут", Ложь);
	СтруктураРезультат.Вставить("FatalError", Ложь);

	СтруктураРезультат.Вставить("чКоличествоПрочитано", 0);	
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	

	Результат = Компонента.ConsumePool(чслТайимаутНаОдно, Количество, ErrorCountToInterrupt);
	Если Результат <> 1 Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	СтруктураРезультат.булТаймаут = (СтруктураРезультат.ОписаниеОшибки = "Local: Timed out") ИЛИ (СтруктураРезультат.ОписаниеОшибки = "Timeout: Local: Timed out")
		ИЛИ (СтруктураРезультат.ОписаниеОшибки = "Local queue is full");
	СтруктураРезультат.Успех = (СтруктураРезультат.булТаймаут ИЛИ Результат = 1) И НЕ Компонента.FatalError;
	СтруктураРезультат.FatalError = Компонента.FatalError;
	
	СтруктураРезультат.чКоличествоПрочитано = Компонента.GetMessagePoolLength();		
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Получатель прочитать список сообщений
//		
//
// Параметры:
//  булПолучатьBase64	- Булево	 - Есдли ИСТИНА - тело сообщения, ключи и значения заголовков -  будут содержать строки в формате Base64 
// 
// Возвращаемое значение:
// - Структура - (Успех, стрСообщения, стрОписнаиеОшибки)
//      Успех 			- Истина, если успешно
//		стрСообщения		- Сообщения в формате JSON
//		ОписаниеОшибки 	- Cодержит описание ошибки, в случае неудачи, при выпонении операции
// 
//Получает сообщения из внутреннего пула ВК в формате JSON
//Если сообщения содержат двоичные данные - необходимо вызывать функцию с флагом булПолучатьBase64 = ИСТИНА, иначе JSON сформировать не удастся
Функция ПолучательПолучитьСообщенияИзПулаJSON(Компонента, Знач булПолучатьBase64) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("СтрокаСообщения", Неопределено);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	//Читаем из пула компоненты	
	СтруктураРезультат.СтрокаСообщения = Компонента.ReceiveJSONMessages(булПолучатьBase64);
	Если  СтруктураРезультат.СтрокаСообщения = Неопределено Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;	

	//Очищаем пул компоненты	
	Результат = Компонента.ClearMessagePool();
	Если Результат <> Истина Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции   

// Функция - Получить сообщения из пула СтрокаВнутр
//		
//
// Параметры:
//  булПолучатьBase64	- Булево	 - Есдли ИСТИНА - тело сообщения, ключи и значения заголовков -  будут содержать строки в формате Base64 
// 
// Возвращаемое значение:
// - Структура - (Успех, стрСообщения, стрОписнаиеОшибки)
//      Успех 			- Истина, если успешно
//		стрСообщения		- Сообщения в формате JSON
//		ОписаниеОшибки 	- Cодержит описание ошибки, в случае неудачи, при выпонении операции
// 
//Получает сообщения из внутреннего пула ВК в формате ЗначениеВСтрокуВнутр 1С
//Если сообщения содержат двоичные данные - необходимо вызывать функцию с флагом булПолучатьBase64 = ИСТИНА, иначе сформировать не удастся
Функция ПолучательПолучитьСообщенияИзПулаСтрокаВнутр(Компонента, Знач булПолучатьBase64) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("стрСообщения", Неопределено);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	//Читаем из пула компоненты	
	СтруктураРезультат.стрСообщения = Компонента.ReceiveOnecInternalMessages(булПолучатьBase64);
	Если  СтруктураРезультат.стрСообщения = Неопределено Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;	

	//Очищаем пул компоненты	
	Результат = Компонента.ClearMessagePool();
	Если Результат <> Истина Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции

// Функция - Получатель получить смещения раздела
//
// Параметры:
//  Топик - Строка	 - Имя топика
//  Раздел	 - Число 	 - Номер раздела
//  Таймаут - Число	 - Время в миллисекундах после которого выполнение будет прервано
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если успешно
//		чМинимальное 		- Минимальное смещение раздела
//		чМинимальное 		- Максимальное смещение раздела
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Возвращает минимальное и максимальное смещение раздела
Функция ПолучательПолучитьСмещенияРаздела(Компонента, Знач Топик, Знач Раздел, Знач Таймаут) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("чМинимальное", -1);
	СтруктураРезультат.Вставить("чМаксимальное", -1);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	Результат = Компонента.QueryWatermarkOffsets(Топик, Раздел, Таймаут);
	Если Результат = Неопределено Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;	

	СтруктураРезультатПреобразования = JSONВСтруктуру(Результат);
	Если Не СтруктураРезультатПреобразования.Успех Тогда
		СтруктураРезультат.ОписаниеОшибки = СтруктураРезультатПреобразования.ОписаниеОшибки;
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	Попытка 
		СтруктураРезультат.чМинимальное = СтруктураРезультатПреобразования.масДанные[0].Low;
		СтруктураРезультат.чМаксимальное = СтруктураРезультатПреобразования.масДанные[0].Hight;
		
	Исключение
		СтруктураРезультат.ОписаниеОшибки = "Ошибка разбора ответа";
		Возврат СтруктураРезультат;
	КонецПопытки;

	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции

// Функция - Получатель получить текущее смещение раздела
//
// Параметры:
//  Топик - Строка	 - Имя топика
//  Раздел	 - Число 	 - Номер раздела
//  Таймаут - Число	 - Время в миллисекундах после которого выполнение будет прервано
//
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если успешно
//		чСмещение 			- текущее смещение для группы
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Получает текущее смещение раздела для группы
Функция ПолучательПолучитьТекущееСмещениеРаздела(Компонента, Знач Топик, Знач Раздел, Знач Таймаут) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("чСмещение", -1);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.чСмещение = Компонента.CommittedOffset(Топик, Раздел, Таймаут);
	Если СтруктураРезультат.чСмещение = Неопределено Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;	

	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции

// Функция - Получатель зафиксировать смещения
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Записывает в брокер последние прочитанные смещения разделов
//Нужно вызывать после вызово функций чтения "ПолучательПолучитьСообщения", "ПолучательПрочитатьСписокСообщений", "ПолучательПолучитьСообщенияJSON"
Функция ПолучательЗафиксироватьСмещения(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.Успех = Компонента.Commit();
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	Возврат СтруктураРезультат;	
КонецФУнкции

Функция ПолучательУдалятьСообщениеИзЛокальнойОчередиПриФормированииJSON(Компонента, Удалять = Ложь) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	Компонента.RemoveMessagesFromLocalQueueOnJSONBuild = Удалять;
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции

Функция ПолучательМаксимальноеКоличествоСообщенийВЛокальнойОчереди(Компонента, Количество = 100000) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	Компонента.MaxMessagesInLocalQueue = Количество;
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции

Функция ПолучательЭкранироватьКлючСообщения(Компонента, Экранировать = Истина) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	Компонента.EscapeMessageKey = Экранировать;
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции

Функция ПолучательЭкранироватьЗначениеСообщения(Компонента, Экранировать = Истина) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	Компонента.EscapeMessageValue = Экранировать;
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции

Функция ПолучательЭкранироватьКлючиЗаголовков(Компонента, Экранировать = Истина) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	Компонента.EscapeMessageHeaderKey = Экранировать;
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции

Функция ПолучательЭкранироватьЗначенияЗаголовков(Компонента, Экранировать = Истина) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	Компонента.EscapeMessageHeaderValue = Экранировать;
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции

Функция ПолучательДобавитьФильтрЗаголовка(Компонента, Знач КлючЗаголовка) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.Успех = Компонента.AppendHeaderFilter(КлючЗаголовка);
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	Возврат СтруктураРезультат;	
КонецФункции

Функция ПолучательОчиститьФильтрыЗаголовков(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	СтруктураРезультат.Успех = Компонента.ClearHeaderFilter();
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	Возврат СтруктураРезультат;	
КонецФункции


// Функция - Получатель закрыть
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Закрывает соединения, завершает управялющие потоки внутри ВК, закрывает объект получателя
Функция ПолучательЗакрыть(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента <> Неопределено Тогда
		СтруктураРезультатСбросаНазначений = ПолучательСброситьНазначения(Компонента);
		Если НЕ СтруктураРезультатСбросаНазначений.Успех Тогда
			СтруктураРезультат.Успех = Ложь;
			СтруктураРезультат.ОписаниеОшибки = СтруктураРезультат.ОписаниеОшибки + СтруктураРезультатСбросаНазначений.ОписаниеОшибки + "; ";
		КонецЕсли;
//		СтруктураРезультатОтписки = ПолучательОтписаться();
//		Если НЕ СтруктураРезультатОтписки.Успех Тогда
//			СтруктураРезультат.Успех = Ложь;
//			СтруктураРезультат.ОписаниеОшибки = СтруктураРезультат.ОписаниеОшибки + СтруктураРезультатСбросаНазначений.ОписаниеОшибки + "; ";
//		КонецЕсли;	
	КонецЕсли;
	
	Компонента = Неопределено;
	
	Возврат СтруктураРезультат;	
КонецФункции

Функция ПолучательПолучитьТаблицуСообщения() Экспорт
	
	ТаблицаРезультат = Новый ТаблицаЗначений();
	
	ТаблицаРезультат.Колонки.Добавить("key", 		);
	ТаблицаРезультат.Колонки.Добавить("value", 		);
	ТаблицаРезультат.Колонки.Добавить("topic", 		Новый ОписаниеТипов("Строка"));
	ТаблицаРезультат.Колонки.Добавить("partition", 	Новый ОписаниеТипов("Число"));
	ТаблицаРезультат.Колонки.Добавить("offset", 	Новый ОписаниеТипов("Число"));
	ТаблицаРезультат.Колонки.Добавить("timestamp", 	Новый ОписаниеТипов("Число"));
	ТаблицаРезультат.Колонки.Добавить("headers", 	Новый ОписаниеТипов("ТаблицаЗначений"));
	
	Возврат ТаблицаРезультат;	
КонецФункции

Функция ПолучательПолучитьТаблицуЗаголовки() Экспорт
	
	ТаблицаРезультат = Новый ТаблицаЗначений();
	
	ТаблицаРезультат.Колонки.Добавить("key", 	);
	ТаблицаРезультат.Колонки.Добавить("value",	);
		
	Возврат ТаблицаРезультат;	
КонецФункции


#КонецОбласти

#Область Отправитель
	
Функция СоздатьОтправителя() Экспорт	
	Возврат ЛогистикаСинхронизацияПовтИсп.СоздатьКомпоненту("KafkaProducer");
КонецФункции	

// Функция - Отправитель проверить доступность объекта 
// 
// Возвращаемое значение:
//   - Структура - (Успех, стрОписнаиеОшибки)
//      Успех 			- Если объект существует
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Проверяет, существует ли объект получателя и возможна ли работа с ним
Функция ОтправительПроверитьДоступностьОбъекта(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Отправитель установить параметр глобальный
//
// Параметры:
//  Ключ		 - 	Строка - Имя параметра
//  Значение	 - 	Строка - Значение параметра
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки)
//      Успех 			- Истина, если параметр установлен
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Устанавливает параметр глабольного контекста для отправителя (Компонента)
//	Пример: Ключ = socket.timeout.ms Значение = 30000  
//!!Для применения установленных параметров - необходимка повторная инициализация получателя с помощью функции "ОтправительИнициализировать"
Функция ОтправительУстановитьПараметрГлобальный(Компонента, Знач Ключ, Знач Значение) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
		
	СтруктураРезультат.Успех = Компонента.SetGlobalConf(Ключ, Значение);
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Отправитель установить параметр топика
//
// Параметры:
//  Ключ		 - 	Строка - Имя параметра
//  Значение	 - 	Строка - Значение параметра
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки)
//      Успех			- Истина, если параметр установлен
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Устанавливает параметр топика для отправителя (Компонента) 
//	Пример: Ключ = auto.offset.reset Значение = smallest  
//!!Для применения установленных параметров - необходимка повторная инициализация получателя с помощью функции "ОтправительИнициализировать"
Функция ОтправительУстановитьПараметрТопика(Компонента, Знач Ключ, Знач Значение) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
		
	СтруктураРезультат.Успех = Компонента.SetTopicConf(Ключ, Значение);
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Отправитель сбросить параметры
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки)
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Сбрасывает установенные параметры топика и параметры глобального контекста (установленные функциями ОтправительУстановитьПараметрГлобальный, ОтправительУстановитьПараметрТопика)
//!!Для применения - необходимка повторная инициализация получателя с помощью функции "ОтправительИнициализировать"
Функция ОтправительСброситьПараметры(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
		
	СтруктураРезультат.Успех = Компонента.ConfReset();
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Отправитель инициализировать
//
// Параметры:
//  Адрес	 - Строка 	 - Адреса брокера в формате "10.0.5.187:9092" 
//  Топик	 - Стркоа	 - Имя топика
//	Раздел   - Число	 - Номер раздела, если -1 - будет выполнено партиционирование по ключу
//
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки)
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Применяет параметры, установленные функциями "ОтправительУстановитьПараметрГлобальный", "ОтправительУстановитьПараметрТопика", "ОтправительСброситьПараметры" 
//Открывает соединения до брокера, создает управляющие потоки внутри ВК, полностью подготавливает отправителя
Функция ОтправительИнициализировать(Компонента, Знач Адрес, Знач Топик, Знач Раздел = -1) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	

	СтруктураРезультат.Успех = Компонента.Initialize(Адрес, Топик, Раздел);
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;              	
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Отправитель загрузить сообщения из JSON в список на отправку
//
// Параметры:
//  стрJSON		 - Строка	 - JSON в формате "[{"Key": "Ключ", "Value": "Сообщение", "Headers": [{"Заголовок1": "ЗначениеЗаголовка1"}]}]"
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки)
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Загружает список сообщений из JSON в внутренний пул ВК на отправку
Функция ОтправительЗагрузитьСообщенияИзJSONВСписокНаОтправку(Компонента, Знач стрJSON) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	

	СтруктураРезультат.Успех = Компонента.SetJSONMessageList(стрJSON);
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;

	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Отправитель получить количество сообщений в списке на отправку
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки)
//      Успех 			- Истина, если успешно
//		чКоличество 		- количество сообщений в списке на отправку
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Возвращает количество сообщенйи в списке на отправку (внутри ВК)
Функция ОтправительПолучитьКоличествоСообщенийВСпискеНаОтправку(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("чКоличество", -1);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	

	СтруктураРезультат.чКоличество = Компонента.GetMessagePoolLength();
	СтруктураРезультат.Успех = СтруктураРезультат.чКоличество >= 0;
	
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;

	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Отправитель очистить список на отправку
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки)
//      Успех 			- Истина, если успешно
//		ОписаниеОшибки 	- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Очищает внутренний пул на отправку ВК
Функция ОтправительОчиститьСписокНаОтправку(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.Успех = Компонента.ClearMessagePool();
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;

	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Отправитель все сообщения доставлены
// 
// Возвращаемое значение:
// - Структура - (Успех, булВсеСообщенияДоставлены, стрОписнаиеОшибки)
//      Успех 					- Истина, если успешно
//		булВсеСообщенияДоставлены	- Истина, если все сообщения из внутреннего пула сообщений ВК были успешно доставлены (после вызова "ОтправительОтправить")
//		ОписаниеОшибки 			- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Получает признак успешности обработки пула сообщений (булВсеСообщенияДоставлены = Истина, ели все сообщения из пула успешно доставлены, после вызова функции "ОтправительОтправить")
Функция ОтправительВсеСообщенияДоставлены(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Истина);
	СтруктураРезультат.Вставить("булВсеСообщенияДоставлены", Ложь);

	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.булВсеСообщенияДоставлены = Компонента.IsDelivered();
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Отправитель получить JSONОтчет о доставке
// (БЫСТРЫЙ СПОСОБ)
// Возвращаемое значение:
// - Структура - (Успех, стрОтчетОДоставке, стрОписнаиеОшибки)
//      Успех 					- Истина, если успешно
//		стрОтчетОДоставке			- JSON отчет о доставке
//		ОписаниеОшибки 			- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Возвращает массив структур в формате JSON - отчет о доставке (колонки: key, status, topic, partition, timestamp, offset, error)
Функция ОтправительПолучитьJSONОтчетОДоставке(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("стрОтчетОДоставке", Неопределено);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.стрОтчетОДоставке = Компонента.GetJSONDeliveryReport();	
	Если СтруктураРезультат.стрОтчетОДоставке = Неопределено Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;	
	КонецЕсли;

	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФункции

// Функция - Отправитель отправить
// 
// Возвращаемое значение:
// - Структура - (Успех, стрОписнаиеОшибки)
//      Успех 					- Истина, если успешно
//		ОписаниеОшибки 			- содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Выполняет отправку из внутреннего пула сообщений ВК, очищает этот пул
Функция ОтправительОтправить(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.Успех = Компонента.Produce();	
	Если СтруктураРезультат.Успех <> Истина Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;	
	КонецЕсли;

	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФункции

Функция ОтправительДекодироватьКлючСообщенияКакBase64Строку(Компонента, Декодировать = Ложь) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	Компонента.DecodeBase64Key = Декодировать;
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции

Функция ОтправительДекодироватьТелоСообщенияКакBase64Строку(Компонента, Декодировать = Ложь) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	Компонента.DecodeBase64Value = Декодировать;
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции

Функция ОтправительДекодироватьЗначенияЗаголовковКакBase64Строку(Компонента, Декодировать = Ложь) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	Компонента.DecodeBase64HeadersValue = Декодировать;
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции


#КонецОбласти

#Область КлиентАдминистрирования

Функция СоздатьКлиентАдминистрирования() Экспорт	
	Возврат ЛогистикаСинхронизацияПовтИсп.СоздатьКомпоненту("KafkaAdminClient");
КонецФункции

// Функция - Инициализировать клиент администрирования
//
// Параметры:
// Компонента - <Тип> - экземпляр компоненты
// СписокБрокеров - Строка - Адреса брокеров в формате "10.0.5.187:9092,10.0.5.85:9092,10.0.5.86:9092"
//
// Возвращаемое значение:
// 	Структура - содержит: 
//      * Успех - булево - Истина, если успешно
//		* ОписаниеОшибки - строка  - содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Применяет параметры, установленные функциями "КлиентАдминистрирования_УстановитьПараметрГлобальный", "КлиентАдминистрирования_СброситьПараметры" 
//Открывает соединения до брокера, создает управляющие потоки внутри ВК
//@skip-check doc-comment-type
Функция КлиентАдминистрированияИнициализация(Компонента, Знач СписокБрокеров) Экспорт
	
	Результат = Новый Структура();
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("ОписаниеОшибки", "");
		
	Результат.Успех = Компонента.Инициализация(СписокБрокеров);
	Если НЕ Результат.Успех = Истина Тогда
		Результат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
		
	Возврат Результат;	
КонецФункции 

// Функция - Клиент администрирования добавить назначение
//
// Параметры:
//  Компонента	 - <Тип> -  экземпляр компоненты
//  Топик	 - Строка	 -  Имя топика, который необходимо добавить в список назначений
//  Раздел	 - Число	 -  Номер раздела топика
//  Смещение	 - Число,Неопределено	 - Смещение раздела
// 
// Возвращаемое значение:
// Структура:
//      *Успех - Булево - Истина, если успешно
//		*ОписаниеОшибки - Строка - содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Добавляет назначение в специальный список, внутри ВК
//@skip-check doc-comment-type
Функция КлиентАдминистрированияДобавитьНазначение(Компонента, Знач Топик, Знач Раздел, Знач Смещение = Неопределено) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.Успех = Компонента.AddRecordToTopicPartitionList(Топик, Раздел, Смещение);
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	Возврат СтруктураРезультат;	
КонецФункции   

// Функция - Клиент администрирования очистить список назначений
//  Параметры:
//  Компонента	 - <Тип> -  экземпляр компоненты
// Возвращаемое значение:
// - Структура:
//      *Успех - Булево - Истина, если успешно
//		*ОписаниеОшибки - Строка - содержит описание ошибки, в случае неудачи, при выпонении операции
//
//Очищает список назначений внутри ВК, сформированный посредставм вызова функции "КлиентАдминистрирования_ДобавитьНазначение"
//@skip-check doc-comment-type
Функция КлиентАдминистрированияОчиститьСписокНазначений(Компонента) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.Успех = Компонента.ClearTopicPartitionList();
	Если СтруктураРезультат.Успех <> Истина Тогда 
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	КонецЕсли;
	
	Возврат СтруктураРезультат;	
КонецФункции

Функция КлиентАдминистрированияУдалитьСообщенияДоСмещения(Компонента, Знач Таймаут = 30000) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("Результат", Неопределено);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	//Удаляем сообщения	
	СтруктураРезультат.Результат = Компонента.DeleteRecordsBefore(Таймаут);
	Если  СтруктураРезультат.Результат = Неопределено Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;	

	//Очищаем список назначений	
	Результат = Компонента.ClearTopicPartitionList();
	Если Результат <> Истина Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции    

Функция КлиентАдминистрированияПолучитьСмещенияГруппыПолучателей(Компонента, Знач Группа, Знач Таймаут = 30000) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("Результат", Неопределено);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	//Удаляем сообщения	
	СтруктураРезультат.Результат = Компонента.GetGroupOffsets(Группа, Таймаут);
	Если СтруктураРезультат.Результат = Неопределено Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции  

Функция КлиентАдминистрированияПолучитьСписокГруппПолучателей(Компонента, Знач Таймаут = 30000) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("Результат", Неопределено);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	//Удаляем сообщения	
	СтруктураРезультат.Результат = Компонента.GetGroupList(Таймаут);            
	СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
	Если СтруктураРезультат.Результат = Неопределено Тогда
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции           

Функция КлиентАдминистрированияПолучитьМетаданные(Компонента, Знач Таймаут = 30000, Знач Топик = Неопределено) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("Результат", Неопределено);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	//Удаляем сообщения	
	СтруктураРезультат.Результат = Компонента.GetMetadata(Таймаут, Топик);            
	Если СтруктураРезультат.Результат = Неопределено Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции 

Функция КлиентАдминистрированияИзменитьСмещенияГруппы(Компонента, Знач Группа, Знач Таймаут = 30000) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("Результат", Неопределено);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	//Изменяем смещения группы	
	СтруктураРезультат.Результат = Компонента.AlterGroupOffsets(Группа, Таймаут);
	Если  СтруктураРезультат.Результат = Неопределено Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;	

	//Очищаем список назначений	
	Результат = Компонента.ClearTopicPartitionList();
	Если Результат <> Истина Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции  

Функция КлиентАдминистрированияУдалитьСмещенияГруппы(Компонента, Знач Группа, Знач Таймаут = 30000) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	СтруктураРезультат.Вставить("Результат", Неопределено);	
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	//Удаляем смещения группы	
	СтруктураРезультат.Результат = Компонента.DeleteGroupOffsets(Группа, Таймаут);
	Если  СтруктураРезультат.Результат = Неопределено Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;	

	//Очищаем список назначений	
	Результат = Компонента.ClearTopicPartitionList();
	Если Результат <> Истина Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФУнкции   

Функция КлиентАдминистрированияПолучитьСмещенияРазделов(Компонента, Знач Таймаут = 30000) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	СтруктураРезультат.Вставить("Результат", Неопределено);		
	
	Если Компонента = Неопределено  Тогда
		СтруктураРезультат.ОписаниеОшибки = "Компонента не инициализирована";
		Возврат СтруктураРезультат;
	КонецЕсли;	
	
	//Получаем смещения разделов	
	СтруктураРезультат.Результат = Компонента.QueryWatermarkOffsets(Таймаут);
	Если СтруктураРезультат.Результат = Неопределено Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;	

	//Очищаем список назначений	
	Результат = Компонента.ClearTopicPartitionList();
	Если Результат <> Истина Тогда
		СтруктураРезультат.ОписаниеОшибки = Компонента.ErrorDescription;
		Возврат СтруктураРезультат;
	КонецЕсли;
	
	СтруктураРезультат.Успех = Истина;
	Возврат СтруктураРезультат;	
КонецФункции
#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

Функция СтруктуруВJSON(СтруктураДанные) Экспорт
	
	СтруктураРезультат = Новый Структура();
	
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	СтруктураРезультат.Вставить("СтруктураДанные", "");
	
	
	ЗаписьJSON                = Новый ЗаписьJSON();
	НастройкиСериализацииJSON = Новый НастройкиСериализацииJSON();
	
	ЗаписьJSON.УстановитьСтроку();
	
	НастройкиСериализацииJSON.ВариантЗаписиДаты      = ВариантЗаписиДатыJSON.ЛокальнаяДата;
	НастройкиСериализацииJSON.ФорматСериализацииДаты = ФорматДатыJSON.ISO;
	
	Попытка
		ЗаписатьJSON(ЗаписьJSON, СтруктураДанные, НастройкиСериализацииJSON);
		
		СтруктураРезультат.СтруктураДанные  = ЗаписьJSON.Закрыть();
		СтруктураРезультат.Успех = Истина;
	Исключение
		СтруктураРезультат.ОписаниеОшибки = ОписаниеОшибки();	
	КонецПопытки;
	
	Возврат СтруктураРезультат;   	
КонецФункции

Функция JSONВСтруктуру(СтрокаJSON)   Экспорт
		
	СтруктураРезультат = Новый Структура();
	
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	СтруктураРезультат.Вставить("МассивДанные", Неопределено);	
	
	ЧтениеJSON = Новый ЧтениеJSON;
	ЧтениеJSON.УстановитьСтроку(СтрокаJSON);
	
	Попытка
		Данные = ПрочитатьJSON(ЧтениеJSON, Ложь);
		
		Если ТипЗнч(Данные) = Тип("Структура") Тогда
			СтруктураРезультат.МассивДанные = Новый Массив();	
			СтруктураРезультат.МассивДанные.Добавить(Данные);
		ИначеЕсли ТипЗнч(Данные) = Тип("Массив") Тогда
			СтруктураРезультат.МассивДанные = Данные;				
		Иначе
			ВызватьИсключение "Не верный тип данных!";
		КонецЕсли;
		
		СтруктураРезультат.Успех = Истина;
	Исключение
		СтруктураРезультат.ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;	
	
	ЧтениеJSON.Закрыть();
	
	Возврат СтруктураРезультат;	
КонецФункции

Функция СтрокаВДвоичныеДанные(СтрокаДляПреобразованияВДвоичныеДанные) Экспорт
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("Успех", Ложь);
	СтруктураРезультат.Вставить("ОписаниеОшибки", "");
	СтруктураРезультат.Вставить("ДвоичныеДанные", Неопределено);
	
	Поток  = Новый ПотокВПамяти();
	Запись = Новый ЗаписьДанных(Поток,КодировкаТекста.UTF8);
	
	Попытка
		Запись.ЗаписатьСимволы(СтрокаДляПреобразованияВДвоичныеДанные);
		Запись.Закрыть(); 
	Исключение
		СтруктураРезультат.ОписаниеОшибки = ОписаниеОшибки(); 
		Возврат СтруктураРезультат;
	КонецПопытки;
	
	СтруктураРезультат.ДвоичныеДанные = Поток.ЗакрытьИПолучитьДвоичныеДанные(); 
	СтруктураРезультат.Успех = Истина;
	
	Возврат СтруктураРезультат; 	
КонецФункции

#КонецОбласти
#КонецОбласти



