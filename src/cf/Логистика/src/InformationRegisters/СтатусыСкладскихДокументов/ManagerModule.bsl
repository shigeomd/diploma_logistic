#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции
Процедура ОпределитьСтатусДокумента(Документ) Экспорт
	
	РасчетныйСтатус = ОпределитьРасчетныйСтатус(Документ);

	МенеджерЗаписи = РегистрыСведений.СтатусыСкладскихДокументов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Документ = Документ;
	МенеджерЗаписи.Прочитать();
	Если НЕ МенеджерЗаписи.Выбран() ИЛИ НЕ МенеджерЗаписи.Статус = РасчетныйСтатус Тогда
		МенеджерЗаписи = РегистрыСведений.СтатусыСкладскихДокументов.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.Документ = Документ;
		МенеджерЗаписи.Статус = РасчетныйСтатус;
		МенеджерЗаписи.Записать();		
	КонецЕсли;	
КонецПроцедуры

Функция ОпределитьРасчетныйСтатус(Документ) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.ДокументОснование,
	|	СУММА(ВложенныйЗапрос.КоличествоДокументов) КАК КоличествоДокументов,
	|	СУММА(ВложенныйЗапрос.КоличествоВыполненныхДокументов) КАК КоличествоВыполненныхДокументов
	|ИЗ
	|	(ВЫБРАТЬ
	|		1 КАК КоличествоДокументов,
	|		ВЫБОР
	|			КОГДА ПриходныйОрдер.Выполнен
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК КоличествоВыполненныхДокументов,
	|		ПриходныйОрдер.ДокументОснование
	|	ИЗ
	|		Документ.ПриходныйОрдер КАК ПриходныйОрдер
	|	ГДЕ
	|		ПриходныйОрдер.ДокументОснование = &ДокументОснование
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|
	|	ВЫБРАТЬ
	|		1,
	|		ВЫБОР
	|			КОГДА РасходныйОрдер.Выполнен
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		РасходныйОрдер.ДокументОснование
	|	ИЗ
	|		Документ.РасходныйОрдер КАК РасходныйОрдер
	|	ГДЕ
	|		РасходныйОрдер.ДокументОснование = &ДокументОснование) КАК ВложенныйЗапрос
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ДокументОснование";
	Запрос.УстановитьПараметр("ДокументОснование", Документ);
	Результат = Запрос.Выполнить();
	РасчетныйСтатус = Перечисления.СтатусыСкладскихДокументов.ПустаяСсылка();
	Если Результат.Пустой() Тогда
		РасчетныйСтатус = Перечисления.СтатусыСкладскихДокументов.Новый; 
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Если Выборка.КоличествоДокументов = Выборка.КоличествоВыполненныхДокументов Тогда
			РасчетныйСтатус = Перечисления.СтатусыСкладскихДокументов.Завершен;
		Иначе
			РасчетныйСтатус = Перечисления.СтатусыСкладскихДокументов.Подготовлен;
		КонецЕсли;
		
	КонецЕсли;
	Возврат РасчетныйСтатус;
КонецФункции
#КонецОбласти

#КонецЕсли