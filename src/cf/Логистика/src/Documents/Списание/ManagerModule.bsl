#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции
// Получает таблицу по товарам для проведения.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.Списание - Ссылка на документ.
//
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица по товарам для проведения.
//
Функция ПолучитьТаблицуПоТоварам(ДокументСсылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.Ссылка.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	Товары.Ссылка КАК Документ,
	|	Товары.Ссылка.Склад КАК Склад,
	|	Товары.Номенклатура КАК Номенклатура,
	|	СУММА(Товары.Количество) КАК Количество
	|ИЗ
	|	Документ.Списание.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка = &ДокументСсылка
	|СГРУППИРОВАТЬ ПО
	|	Товары.Ссылка.Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	Товары.Ссылка,
	|	Товары.Ссылка.Склад,
	|	Товары.Номенклатура";
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

Процедура ВыполнитьКонтроль(ТаблицаТовары, Отказ) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	втТовары.Склад КАК Склад,
	|	втТовары.Номенклатура КАК Номенклатура,
	|	втТовары.Количество КАК Количество
	|ПОМЕСТИТЬ втТовары
	|ИЗ
	|	&ТаблицаТовары КАК втТовары
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПРЕДСТАВЛЕНИЕ(втТовары.Склад) КАК Склад,
	|	ПРЕДСТАВЛЕНИЕ(втТовары.Номенклатура) КАК Номенклатура,
	|	втТовары.Количество,
	|	ТоварыНаСкладахОстатки.КоличествоОстаток
	|ИЗ
	|	втТовары КАК втТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ТоварыНаСкладах.Остатки(, (Склад, Номенклатура) В
	|			(ВЫБРАТЬ
	|				втТовары.Склад,
	|				втТовары.Номенклатура
	|			ИЗ
	|				втТовары КАК втТовары)) КАК ТоварыНаСкладахОстатки
	|		ПО втТовары.Склад = ТоварыНаСкладахОстатки.Склад
	|		И втТовары.Номенклатура = ТоварыНаСкладахОстатки.Номенклатура
	|ГДЕ
	|	втТовары.Количество > ЕСТЬNULL(ТоварыНаСкладахОстатки.КоличествоОстаток, 0)";
	Запрос.УстановитьПараметр("ТаблицаТовары", ТаблицаТовары);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Отказ = Истина;
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстСообщения = СтрШаблон("Недостаточно товара %1 на складе %2. Требуется %3, доступно %4.", 
					Выборка.Номенклатура, 
					Выборка.Склад,
					Выборка.Количество,
					Выборка.КоличествоОстаток);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЦикла;
	КонецЕсли;	
КонецПроцедуры
#КонецОбласти

#КонецЕсли