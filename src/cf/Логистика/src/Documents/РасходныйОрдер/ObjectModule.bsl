#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено Тогда // Ввод нового.
		Ответственный = Пользователи.ТекущийПользователь();
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Списание") Тогда
		ДокументОснование = ДанныеЗаполнения;
		ДокументОснованиеПредставление = Строка(ДанныеЗаполнения);
		Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "Склад");
		ЗаполнитьПоОстаткамТоварыКРасходуСоСклада(ДанныеЗаполнения); 
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Перемещение") Тогда
		ДокументОснование = ДанныеЗаполнения;
		ДокументОснованиеПредставление = Строка(ДанныеЗаполнения);
		Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "Склад");
		ЗаполнитьПоОстаткамТоварыКРасходуСоСклада(ДанныеЗаполнения);
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаПроведения()
	Движения.ТоварыКРасходуСоСклада.Записывать = Истина;
	Движения.ТоварыНаСкладах.Записывать = Истина;

	СтруктураУзлаОбмена = ЛогистикаСинхронизацияСервер.ПолучитьУзелОбмена(Перечисления.НазначениеУзлаОбмена.Документы);

	СтруктураТаблиц = Документы.РасходныйОрдер.ПолучитьТаблицыДвижений(Ссылка, СтруктураУзлаОбмена.ЦентральныйОфис);
	
	Движения.ТоварыКРасходуСоСклада.Загрузить(СтруктураТаблиц.ТаблицаТоварыКРасходуСоСклада);
	Движения.ТоварыНаСкладах.Загрузить(СтруктураТаблиц.ТаблицаТоварыНаСкладах);	

	РегистрыСведений.СтатусыСкладскихДокументов.ОпределитьСтатусДокумента(ДокументОснование);
КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура ЗаполнитьПоОстаткамТоварыКРасходуСоСклада(Документ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыКРасходуОстатки.Номенклатура КАК Номенклатура,
	|	ТоварыКРасходуОстатки.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыКРасходуСоСклада.Остатки(, Документ = &Документ
	|	И Склад = &Склад) КАК ТоварыКРасходуОстатки
	|ГДЕ
	|	ТоварыКРасходуОстатки.КоличествоОстаток > 0";	
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("Склад", Склад);	
	РезультатЗапроса = Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли