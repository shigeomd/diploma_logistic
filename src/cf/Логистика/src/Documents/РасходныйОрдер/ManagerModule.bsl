#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции
// Получает таблицу по товарам для проведения.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.РасходныйОрдер - Ссылка на документ.
//
// Возвращаемое значение:
//  Структура - Структура с таблицами для проведения.
//
Функция ПолучитьТаблицыДвижений(ДокументСсылка, ЦентральныйОфис) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка.Дата КАК Период,
	|	ТаблицаТовары.Ссылка.ДокументОснование КАК Документ,
	|	ТаблицаТовары.Ссылка.Склад КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество,
	|	РасходныйОрдер.Выполнен
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	Документ.РасходныйОрдер.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходныйОрдер КАК РасходныйОрдер
	|		ПО ТаблицаТовары.Ссылка = РасходныйОрдер.Ссылка
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументСсылка
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Ссылка.Дата,
	|	ТаблицаТовары.Ссылка.ДокументОснование,
	|	ТаблицаТовары.Ссылка.Склад,
	|	ТаблицаТовары.Номенклатура,
	|	РасходныйОрдер.Выполнен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.Период,
	|	Значение(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	втДанные.Документ,
	|	втДанные.Склад,
	|	втДанные.Номенклатура,
	|	втДанные.Количество
	|ИЗ
	|	втДанные КАК втДанные
	|ГДЕ
	|	&ЦентральныйОфис
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.Период,
	|	Значение(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	втДанные.Склад,
	|	втДанные.Номенклатура,
	|	втДанные.Количество
	|ИЗ
	|	втДанные КАК втДанные
	|ГДЕ
	|	втДанные.Выполнен";
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ЦентральныйОфис", ЦентральныйОфис);
	РезультатПакет = Запрос.ВыполнитьПакет();
	СтруктураОтвет = Новый Структура;
	СтруктураОтвет.Вставить("ТаблицаТоварыКРасходуСоСклада", РезультатПакет[1].Выгрузить());
	СтруктураОтвет.Вставить("ТаблицаТоварыНаСкладах", РезультатПакет[2].Выгрузить());
	Возврат СтруктураОтвет;
КонецФункции
#КонецОбласти

#КонецЕсли