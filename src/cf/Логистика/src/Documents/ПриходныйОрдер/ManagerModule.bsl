#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныеПроцедурыИФункции
// Получает таблицу по товарам для проведения.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ПриходныйОрдер - Ссылка на документ.
//
// Возвращаемое значение:
//  Структура - Структура с таблицами для проведения.
//
Функция ПолучитьТаблицыДвижений(ДокументСсылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаТовары.Ссылка.Дата КАК Период,
	|	ТаблицаТовары.Ссылка.ДокументОснование КАК Документ,
	|	ТаблицаТовары.Ссылка.Склад КАК Склад,
	|	ТаблицаТовары.Номенклатура КАК Номенклатура,
	|	СУММА(ТаблицаТовары.Количество) КАК Количество,
	|	ПриходныйОрдер.Выполнен
	|ПОМЕСТИТЬ втДанные
	|ИЗ
	|	Документ.ПриходныйОрдер.Товары КАК ТаблицаТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПриходныйОрдер КАК ПриходныйОрдер
	|		ПО ТаблицаТовары.Ссылка = ПриходныйОрдер.Ссылка
	|ГДЕ
	|	ТаблицаТовары.Ссылка = &ДокументСсылка
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаТовары.Ссылка.Дата,
	|	ТаблицаТовары.Ссылка.ДокументОснование,
	|	ТаблицаТовары.Ссылка.Склад,
	|	ТаблицаТовары.Номенклатура,
	|	ПриходныйОрдер.Выполнен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	втДанные.Документ,
	|	втДанные.Склад,
	|	втДанные.Номенклатура,
	|	втДанные.Количество
	|ИЗ
	|	втДанные КАК втДанные
	|ГДЕ
	|	НЕ втДанные.Документ = НЕОПРЕДЕЛЕНО
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втДанные.Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	втДанные.Склад,
	|	втДанные.Номенклатура,
	|	втДанные.Количество
	|ИЗ
	|	втДанные КАК втДанные
	|ГДЕ
	|	втДанные.Выполнен";
	Запрос.УстановитьПараметр("ДокументСсылка", ДокументСсылка);
	РезультатПакет = Запрос.ВыполнитьПакет();
	СтруктураОтвет = Новый Структура;
	СтруктураОтвет.Вставить("ТаблицаТоварыКПоступлениюНаСклад", РезультатПакет[1].Выгрузить());
	СтруктураОтвет.Вставить("ТаблицаТоварыНаСкладах", РезультатПакет[2].Выгрузить());
	Возврат СтруктураОтвет;
КонецФункции
#КонецОбласти

#КонецЕсли