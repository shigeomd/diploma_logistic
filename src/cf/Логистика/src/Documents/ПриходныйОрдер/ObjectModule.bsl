#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено Тогда // Ввод нового.
		Ответственный = Пользователи.ТекущийПользователь();
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Оприходование") Тогда
		ДокументОснование = ДанныеЗаполнения;
		Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "Склад");
		ЗаполнитьПоОстаткамТоварыКПоступлениюНаСклад(ДанныеЗаполнения); 
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.Перемещение") Тогда
		ДокументОснование = ДанныеЗаполнения;
		Склад = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеЗаполнения, "СкладПоступления");
		ЗаполнитьПоОстаткамТоварыКПоступлениюНаСклад(ДанныеЗаполнения);
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаПроведения()
	Движения.ТоварыКПоступлениюНаСклад.Записывать = Истина;
	Движения.ТоварыНаСкладах.Записывать = Истина;

	СтруктураТаблиц = Документы.ПриходныйОрдер.ПолучитьТаблицыДвижений(Ссылка);
	
	Движения.ТоварыКПоступлениюНаСклад.Загрузить(СтруктураТаблиц.ТаблицаТоварыКПоступлениюНаСклад);
	Движения.ТоварыНаСкладах.Загрузить(СтруктураТаблиц.ТаблицаТоварыНаСкладах);

КонецПроцедуры
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Процедура ЗаполнитьПоОстаткамТоварыКПоступлениюНаСклад(Документ)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТоварыКПоступлениюНаСклад.Номенклатура КАК Номенклатура,
	|	ТоварыКПоступлениюНаСклад.КоличествоОстаток КАК Количество
	|ИЗ
	|	РегистрНакопления.ТоварыКПоступлениюНаСклад.Остатки(, Документ = &Документ
	|	И Склад = &Склад) КАК ТоварыКПоступлениюНаСклад
	|ГДЕ
	|	ТоварыКПоступлениюНаСклад.КоличествоОстаток > 0";	
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.УстановитьПараметр("Склад", Склад);	
	РезультатЗапроса = Запрос.Выполнить();	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		НоваяСтрока = Товары.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетальныеЗаписи);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
	ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли