// MIT License

// Copyright (c) 2025 Zherebtsov Nikita <nikita@crimsongold.ru>

// Permission is hereby granted, free of charge, to any person obtaining a copy
// of this software and associated documentation files (the "Software"), to deal
// in the Software without restriction, including without limitation the rights
// to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
// copies of the Software, and to permit persons to whom the Software is
// furnished to do so, subject to the following conditions:

// The above copyright notice and this permission notice shall be included in all
// copies or substantial portions of the Software.

// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
// IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
// FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
// AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
// LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
// OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
// SOFTWARE.

// https://github.com/crimsongoldteam/md_design

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОписаниеТаблицы = Параметры.ОписаниеТаблицы;    
	Если ОписаниеТаблицы <> Неопределено Тогда
		ПостроитьКолонкиПоОписаниюТаблицы(ОписаниеТаблицы, ТаблицаКолонки.ПолучитьЭлементы());	
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	Если ТаблицаКолонки.ПолучитьЭлементы().Количество() = 0 Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "В таблице отсутствуют колонки.";
		Сообщение.Сообщить();             
		
		Возврат;
	КонецЕсли;
	
	Если НЕ ПроверитьГруппыКолонокБезКолонок(ТаблицаКолонки.ПолучитьЭлементы()) Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ПолучитьНовоеОписаниеТаблицы(Параметры.ОписаниеТаблицы);
	Закрыть(Результат);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьКолонку(Команда)
	ДобавитьСтрокуВТаблицу(ПолучитьСтрокуРодитель());
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПодколонку(Команда)
	ТекущиеДанные = Элементы.ТаблицаКолонки.ТекущиеДанные;	
	
	ТекущийРодитель = ТаблицаКолонки;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущийРодитель = ТекущиеДанные;
	КонецЕсли;
	
	ДобавитьСтрокуВТаблицу(ТекущийРодитель);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаКолонки

&НаКлиенте
Процедура КолонкиПриНачалеРедактирования(Элемент, НоваяСтрока)
	Если НоваяСтрока Тогда
		ТекущиеДанные = Элемент.ТекущиеДанные;	
		ТекущиеДанные.ГоризонтальноеПоложение = "Лево";  
		
		ТекущиеДанные.ОписаниеКолонки = ПолучитьНовоеОписаниеКолонки();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаКолонкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	
	ДобавитьСтрокуВТаблицу(ПолучитьСтрокуРодитель(), Копирование);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Модули

&НаКлиенте
Функция СлужебныеФункции()
	Возврат ВладелецФормы.СлужебныеФункции();
КонецФункции

#КонецОбласти 

&НаКлиенте
Функция ПолучитьНовоеОписаниеКолонки()
	ОписаниеКолонки = ВладелецФормы.СоздатьНовуюКолонкуТаблицы();
	ОписаниеКолонки.УИД = Строка(Новый УникальныйИдентификатор);
	ОписаниеКолонки.УИДАтрибута = ОписаниеКолонки.УИД;
	ОписаниеКолонки.ЕстьЗначение = Истина;
	Возврат ОписаниеКолонки;
КонецФункции

&НаКлиенте
Функция ПроверитьГруппыКолонокБезКолонок(ТаблицаКолонкиЭлементы)
	Результат = Истина;
	Для Каждого СтрокаКолонка Из ТаблицаКолонкиЭлементы Цикл
		
		ПодЭлементы = СтрокаКолонка.ПолучитьЭлементы();
		Если ПодЭлементы.Количество() = 0
			И СтрокаКолонка.ЭтоГруппа Тогда

			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтрШаблон("У группы колонок ""%1"" отсутствуют подколонки", СтрокаКолонка.Заголовок);
			Сообщение.Сообщить();             
			
			Результат = Ложь;
			Продолжить;
		КонецЕсли;
		
		Если НЕ ПроверитьГруппыКолонокБезКолонок(ПодЭлементы) Тогда
			Результат = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Функция ПолучитьСтрокуРодитель()     
	ТекущиеДанные = Элементы.ТаблицаКолонки.ТекущиеДанные;	
	
	ТекущийРодитель = Неопределено;
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущийРодитель = ТекущиеДанные.ПолучитьРодителя();
	КонецЕсли;
	
	Если ТекущийРодитель = Неопределено Тогда
		ТекущийРодитель = ТаблицаКолонки;
	КонецЕсли;

	Возврат ТекущийРодитель;
КонецФункции

&НаКлиенте
Процедура ДобавитьСтрокуВТаблицу(ТекущийРодитель, Копирование = Ложь)
	
	НоваяСтрока = ТекущийРодитель.ПолучитьЭлементы().Добавить();  
	
	Если Копирование Тогда
		ТекущиеДанные = Элементы.ТаблицаКолонки.ТекущиеДанные;	
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные,, "УИД");
	Иначе
		НоваяСтрока.ГоризонтальноеПоложение = "Лево";
	КонецЕсли;
	
	НоваяСтрока.ОписаниеКолонки = ПолучитьНовоеОписаниеКолонки();
	
	Элементы.ТаблицаКолонки.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
	Элементы.ТаблицаКолонки.ИзменитьСтроку();

КонецПроцедуры

&НаКлиенте
Процедура ПостроитьКолонкиПоОписаниюТаблицы(ОписаниеЭлемента, ТаблицаКолонкиЭлементы)
	Для Каждого ОписаниеКолонки Из ОписаниеЭлемента.Колонки Цикл
		СтрокаТаблицы = ТаблицаКолонкиЭлементы.Добавить();
		СтрокаТаблицы.ОписаниеКолонки = ОписаниеКолонки;

		Если ОписаниеКолонки.НаборСвойств.Свойство("Заголовок") Тогда
			СтрокаТаблицы.Заголовок = ОписаниеКолонки.НаборСвойств.Заголовок;
		КонецЕсли;
		
		Если ОписаниеКолонки.НаборСвойств.Свойство("ГоризонтальноеПоложение") Тогда
			СтрокаТаблицы.ГоризонтальноеПоложение = ОписаниеКолонки.НаборСвойств.ГоризонтальноеПоложение;
		Иначе
			СтрокаТаблицы.ГоризонтальноеПоложение = "Лево";
		КонецЕсли;
		СтрокаТаблицы.ЭтоГруппа = (ОписаниеКолонки.Тип = "ГруппаКолонокТаблицы");
		
		ПостроитьКолонкиПоОписаниюТаблицы(ОписаниеКолонки, СтрокаТаблицы.ПолучитьЭлементы());
		
		// Сбрасываем иерархию
		ОписаниеКолонки.Колонки = Новый Массив;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьНовоеОписаниеТаблицы(ТекущееОписание)
	СоответствиеКолонок = Новый Соответствие;
	
	Результат = Параметры.ОписаниеТаблицы; 
	
	Результат.Колонки = Новый Массив;

	ЗаполнитьКолонкиВРезультате(Результат, ТаблицаКолонки.ПолучитьЭлементы());
	
	Возврат Результат;
КонецФункции

&НаКлиенте
Процедура ЗаполнитьКолонкиВРезультате(ОписаниеЭлемента, ТаблицаКолонкиЭлементы)
	Для Каждого СтрокаКолонка Из ТаблицаКолонкиЭлементы Цикл
		ТекущаяКолонка = СтрокаКолонка.ОписаниеКолонки;
		ТекущаяКолонка.НаборСвойств.Вставить("Заголовок", СтрокаКолонка.Заголовок);    
		
		ТекущаяКолонка.НаборСвойств.Удалить("ГоризонтальноеПоложение");
		Если СтрокаКолонка.ГоризонтальноеПоложение <> "Лево" Тогда
			ТекущаяКолонка.НаборСвойств.Вставить("ГоризонтальноеПоложение", СтрокаКолонка.ГоризонтальноеПоложение);
		КонецЕсли;
		
		ТекущаяКолонка.Тип = ?(СтрокаКолонка.ЭтоГруппа,"ГруппаКолонокТаблицы", "КолонкаТаблицы"); 
		ОписаниеЭлемента.Колонки.Добавить(ТекущаяКолонка);    
		
		ЗаполнитьКолонкиВРезультате(ТекущаяКолонка, СтрокаКолонка.ПолучитьЭлементы());
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
