#Область СлужебныйПрограммныйИнтерфейс

Процедура ИсполняемыеСценарии() Экспорт
	
	ЮТТесты.ДобавитьТестовыйНабор("Контроль остатков").ВТранзакции()
		.ДобавитьТест("ПолучитьТаблицуПоТоварам")
		.ДобавитьТест("ВыполнитьКонтрольПозитив")
		.ДобавитьТест("ВыполнитьКонтрольНегатив")
	;
	
КонецПроцедуры

Процедура ПолучитьТаблицуПоТоварам() Экспорт
	
	НоваяНоменклатура = ЮТест.Данные().КонструкторОбъекта("Справочник.Номенклатура")
						.ФикцияОбязательныхПолей()
						.Установить("Наименование", "Товар1")
						.Записать();
						
	НовыйСклад = ЮТест.Данные().КонструкторОбъекта("Справочник.Склады")
						.ФикцияОбязательныхПолей()
						.Установить("Наименование", "Склад1")
						.Записать();
	
	ДокументСписания = ЮТест.Данные().КонструкторОбъекта("Документ.Списание")
						.ФикцияОбязательныхПолей()
						.Установить("Дата", ТекущаяДатаСеанса())
						.Установить("Склад", НовыйСклад)
						.ТабличнаяЧасть("Товары")
							.ДобавитьСтроку()
								.Установить("Номенклатура", НоваяНоменклатура)
								.Установить("Количество", 10)
						.Записать();
	
	ТаблицаТоваров = Документы.Списание.ПолучитьТаблицуПоТоварам(ДокументСписания);
	
	ЮТест.ОжидаетЧто(ТаблицаТоваров)
			.ИмеетТип("ТаблицаЗначений");
КонецПроцедуры

Процедура ВыполнитьКонтрольПозитив() Экспорт
	
	НоваяНоменклатура = ЮТест.Данные().КонструкторОбъекта("Справочник.Номенклатура")
						.ФикцияОбязательныхПолей()
						.Установить("Наименование", "Товар1")
						.Записать();
						
	НовыйСклад = ЮТест.Данные().КонструкторОбъекта("Справочник.Склады")
						.ФикцияОбязательныхПолей()
						.Установить("Наименование", "Склад1")
						.Записать();
	
	//формируем остаток
	ДокументОприходования = ЮТест.Данные().КонструкторОбъекта("Документ.Оприходование")
						.ФикцияОбязательныхПолей()
						.Установить("Дата", ТекущаяДатаСеанса()-1000)
						.Установить("Склад", НовыйСклад)
						.ТабличнаяЧасть("Товары")
							.ДобавитьСтроку()
								.Установить("Номенклатура", НоваяНоменклатура)
								.Установить("Количество", 10)
						.Провести();
						
	ДокументПОТ = ЮТест.Данные().КонструкторОбъекта("Документ.ПриходныйОрдер")
						.ФикцияОбязательныхПолей()
						.Установить("Дата", ТекущаяДатаСеанса()-1000)
						.Установить("Склад", НовыйСклад)
						.Установить("ДокументОснование", ДокументОприходования)
						.Установить("Выполнен", Истина)
						.ТабличнаяЧасть("Товары")
							.ДобавитьСтроку()
								.Установить("Номенклатура", НоваяНоменклатура)
								.Установить("Количество", 10)
						.Провести();
	
	ДокументСписания = ЮТест.Данные().КонструкторОбъекта("Документ.Списание")
						.ФикцияОбязательныхПолей()
						.Установить("Дата", ТекущаяДатаСеанса())
						.Установить("Склад", НовыйСклад)
						.ТабличнаяЧасть("Товары")
							.ДобавитьСтроку()
								.Установить("Номенклатура", НоваяНоменклатура)
								.Установить("Количество", 10)
						.Записать();
						
	ЮТест.ОжидаетЧто(ДокументСписания.ПолучитьОбъект())
			.Метод("Записать", ЮТМетоды.МассивПараметров(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный))
			.НеВыбрасываетИсключение();
КонецПроцедуры

Процедура ВыполнитьКонтрольНегатив() Экспорт
	
	НоваяНоменклатура = ЮТест.Данные().КонструкторОбъекта("Справочник.Номенклатура")
						.ФикцияОбязательныхПолей()
						.Установить("Наименование", "Товар1")
						.Записать();
						
	НовыйСклад = ЮТест.Данные().КонструкторОбъекта("Справочник.Склады")
						.ФикцияОбязательныхПолей()
						.Установить("Наименование", "Склад1")
						.Записать();

	//формируем остаток
	ДокументОприходования = ЮТест.Данные().КонструкторОбъекта("Документ.Оприходование")
						.ФикцияОбязательныхПолей()
						.Установить("Дата", ТекущаяДатаСеанса()-1000)
						.Установить("Склад", НовыйСклад)
						.ТабличнаяЧасть("Товары")
							.ДобавитьСтроку()
								.Установить("Номенклатура", НоваяНоменклатура)
								.Установить("Количество", 10)
						.Провести();
	
	ДокументПОТ = ЮТест.Данные().КонструкторОбъекта("Документ.ПриходныйОрдер")
						.ФикцияОбязательныхПолей()
						.Установить("Дата", ТекущаяДатаСеанса()-1000)
						.Установить("Склад", НовыйСклад)
						.Установить("ДокументОснование", ДокументОприходования)
						.Установить("Выполнен", Истина)
						.ТабличнаяЧасть("Товары")
							.ДобавитьСтроку()
								.Установить("Номенклатура", НоваяНоменклатура)
								.Установить("Количество", 10)
						.Провести();
	
	ДокументСписания = ЮТест.Данные().КонструкторОбъекта("Документ.Списание")
						.ФикцияОбязательныхПолей()
						.Установить("Дата", ТекущаяДатаСеанса())
						.Установить("Склад", НовыйСклад)
						.ТабличнаяЧасть("Товары")
							.ДобавитьСтроку()
								.Установить("Номенклатура", НоваяНоменклатура)
								.Установить("Количество", 11)
						.Записать();
						
	ЮТест.ОжидаетЧто(ДокументСписания.ПолучитьОбъект())
			.Метод("Записать", ЮТМетоды.МассивПараметров(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Оперативный))
			.ВыбрасываетИсключение("Не удалось провести");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти
